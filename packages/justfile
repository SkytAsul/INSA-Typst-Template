output_dir_suffix := "build"
report := "silky-report-insa"
letter := "silky-letter-insa"
slides := "silky-slides-insa"

[group("report")]
build-report version: (_build-package version report "document-template.typ" "assets/footer.png" "assets/rennes/*" "assets/hdf/* assets/cvl/*")

[group("letter")]
build-letter version: (_build-package version letter "letter-template.typ" "assets/footer.png" "assets/rennes/logo.png" "assets/hdf/logo.png" "assets/cvl/logo.png")

[group("slides")]
build-slides version: (_build-package version slides "slides-template.typ" "assets/footer.png" "assets/rennes/logo-white.png" "assets/hdf/logo-white.png" "assets/cvl/logo-white.png" "assets/slide*")

[group("report")]
clean-report: (_clean-package report)

[group("letter")]
clean-letter: (_clean-package letter)

[group("slides")]
clean-slides: (_clean-package slides)

[group("report")]
install-report version: (build-report version) (_install-package version report)

[group("letter")]
install-letter version: (build-letter version) (_install-package version letter)

[group("slides")]
install-slides version: (build-slides version) (_install-package version slides)

_build-package version package package_file +assets: (_clean-package package)
    #!/usr/bin/env bash
    set -euo pipefail
    shopt -s extglob

    out_dir={{package / output_dir_suffix / version}}
    mkdir -p "$out_dir"
    mkdir -p "$out_dir/assets"
    cp "../insa-template/{{package_file}}" $out_dir
    out_dir_abs="$(cd $out_dir; pwd)"
    cp -r {{package / "!(build)"}} $out_dir
    (
        cd "../insa-template"
        for asset in {{assets}}; do
            cp --parents "$asset" "$out_dir_abs/"
        done
    )
    echo "Resources copied."

    # for file in "$out_dir/*"; do
    #     sed -i "s/{{{{VERSION}}/{{version}}/g" $file
    # done
    just _file_substitute "{{{{VERSION}}" {{version}} $out_dir
    echo "Version updated."
    echo "Built {{package}}."

_clean-package package:
    rm -rf {{package / output_dir_suffix}}
    @echo "Cleaned {{package}}."

_install-package version package:
    cp -r {{package / output_dir_suffix / version}} ~/.local/share/typst/packages/local/{{package}}/
    @echo "Installed {{package}}."

_file_substitute from to directory:
    #!/usr/bin/env bash
    set -euo pipefail

    for file in {{directory / "*"}}; do
        if [ -d "$file" ]; then
            just _file_substitute {{from}} {{to}} $file
        elif grep -Iq . "$file"; then
            # means the file is not a binary file
            set +e
            occ=$(grep -o {{from}} $file | wc -l)
            set -e
            echo "Substituting $occ occurences of {{from}} in $file."
            sed -i {{"s"/from/to/"g"}} $file
        fi
    done